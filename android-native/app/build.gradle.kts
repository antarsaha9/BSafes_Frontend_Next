import com.github.gradle.node.npm.proxy.ProxySettings
import com.github.gradle.node.npm.task.NpmTask
import org.gradle.api.tasks.Delete
import org.gradle.api.tasks.Copy

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.jetbrains.kotlin.android)
    alias(libs.plugins.node.gradle)
}

android {
    namespace = "com.bsafes.android"
    compileSdk = 34

    androidResources {
        ignoreAssetsPattern = "!.svn:!.git:!.ds_store:!*.scc:!CVS:!thumbs.db:!picasa.ini:!*~"
    }   

    defaultConfig {
        applicationId = "com.bsafes.android"
        minSdk = 24
        targetSdk = 34
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary = true
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    buildFeatures {
        compose = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.1"
    }
    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.material3)
    implementation(libs.billing.ktx)
    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.ui.test.junit4)
    debugImplementation(libs.androidx.ui.tooling)
    debugImplementation(libs.androidx.ui.test.manifest)
    implementation(libs.androidx.webkit)
    implementation(libs.accompanist.permissions)

}


// Define the common path prefix
val pathPrefix = "../../web"

/**
 * Node plugin configuration
 * https://github.com/node-gradle/gradle-node-plugin/blob/master/docs/usage.md#configuring-the-plugin
 */
node {
    // The Node.js project directory location
    // This is where the package.json file and node_modules directory are located
    nodeProjectDir.set(file(pathPrefix))

}

/**
 * This task builds the vite project at ../../web folder.
 */
val buildWeb by tasks.registering(NpmTask::class) {
    description = "Runs vite build"
    args = listOf("run", "build")
    doFirst {
        println("Executing `npm run build` in ../../web")
    }
//    dependsOn("npmInstall")
}

/**
 * Removes the assets generated by the `buildWeb` task.
 */
val cleanWeb by tasks.registering(Delete::class) {
    description = "Removes web assets generated by vite"
    delete("$pathPrefix/dist")
}

/**
 * This task cleans the Android assets folder.
 * This is necessary because the Android build process does not clean the assets folder.
 */
val cleanAssets by tasks.registering(Delete::class) {

    description = "Removes web assets from app module"
    delete("src/main/assets/dist")
}

/**
 * This task copies the the vite project at `../../web/dist` folder to the Android assets folder.
 */
val copyDistToAssets by tasks.registering(Copy::class) {
    description = "Copies web assets into app module"
    from("$pathPrefix/dist")
    into("./src/main/assets/dist")
    dependsOn(buildWeb, cleanAssets)

}

/**
 * Hooks tasks into running as part of the app module build.
 */
//tasks.named("preBuild") {
//    dependsOn(copyDistToAssets)
//}
//
///**
// * Includes tasks when running a project clean.
// */
//tasks.named("clean") {
//    dependsOn(cleanWeb)
//}